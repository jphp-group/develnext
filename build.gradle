def javaVersion    = '1.8';
def projectVersion = '0.9.0';
def jphpVersion    = "0.9.0-SNAPSHOT";

rootProject.ext.jphpVersion = jphpVersion;

if (Boolean.getBoolean("release")) {
    jphpVersion = projectVersion
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = 2.4;
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'signing'
    apply plugin: 'java'
    apply plugin: 'maven'

    project.group = 'org.develnext'
    project.version = projectVersion

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    task sourcesJar(type: Jar, dependsOn: 'classes') {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    configurations {
        provided
    }

    sourceSets {
        main { compileClasspath += configurations.provided }
    }
}

configure([
        project(':jphp-gui-ext'),
        project(':jphp-game-ext'),
        project(':jphp-gui-designer-ext'),
        project(':jphp-app-framework')
]) {
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    javadoc.failOnError = false

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    jar.dependsOn += [sourcesJar, javadocJar]

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    signing {
        sign configurations.archives
    }

    bintray {
        user = rootProject.hasProperty('bintray.user') ? rootProject.getProperty("bintray.user") : "";
        key = rootProject.hasProperty('bintray.apikey') ? rootProject.getProperty("bintray.apikey") : "";

        configurations = ['archives']

        pkg {
            repo = "maven"
            name = "jphp-compiler"
            websiteUrl = 'http://github.com/jphp-compiler'
            vcsUrl = 'http://github.com/jphp-compiler'
            licenses = ["Apache-2.0"]
            publish = true
        }
    }
}

project(':jphp-gui-ext') {
    dependencies {
        compile "org.develnext.jphp:jphp-runtime:$jphpVersion"
        compile "org.develnext.jphp:jphp-xml-ext:$jphpVersion"
    }
}

project(':jphp-game-ext') {
    dependencies {
        compile 'org.dyn4j:dyn4j:3.2.1'

        compile project(':jphp-gui-ext')
        compile "org.develnext.jphp:jphp-runtime:$jphpVersion"
    }
}

project(':jphp-desktop-ext') {
    dependencies {
        compile project(':jphp-gui-ext')
        compile "org.develnext.jphp:jphp-runtime:$jphpVersion"
    }
}

project(':jphp-parser') {
    dependencies {
        compile "org.develnext.jphp:jphp-core:$jphpVersion"
    }
}

project(':jphp-app-framework') {
    dependencies {
        compile project(':jphp-gui-ext')
        compile "org.develnext.jphp:jphp-core:$jphpVersion"
    }
}

project(':develnext') {
    dependencies {
        compile project(':jphp-gui-designer-ext')
        compile project(':jphp-gui-richtext-ext')
        compile project(':jphp-app-framework')
        compile project(':jphp-desktop-ext')
        compile project(':jphp-game-ext')
        compile project(':jphp-parser')
        compile project(':develnext-doc')

        if (new File("./develnext-control/build.gradle").exists()) {
            compile project(':develnext-control')
        }

        // bundles.
        //compile project(':develnext-bundles/dn-orientdb-bundle')
        compile project(':develnext-bundles/dn-jsoup-bundle')
        compile project(':develnext-bundles/dn-mail-bundle')

        compile "org.develnext.jphp:jphp-xml-ext:$jphpVersion"
        compile "org.develnext.jphp:jphp-json-ext:$jphpVersion"
        compile "org.develnext.jphp:jphp-jsoup-ext:$jphpVersion"
        compile "org.develnext.jphp:jphp-zend-ext:$jphpVersion"
        compile "org.develnext.jphp:jphp-sql-ext:$jphpVersion"
        compile "org.develnext.jphp:jphp-compress-ext:$jphpVersion"
        compile "org.develnext.jphp:jphp-compress-ext:$jphpVersion"
        compile "org.develnext.jphp:jphp-debugger:$jphpVersion"
    }
}

