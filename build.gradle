def javaVersion    = '1.7';
def projectVersion = '0.7.2';
def jphpVersion    = projectVersion + "-SNAPSHOT";

if (Boolean.getBoolean("release")) {
    jphpVersion = projectVersion
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = 2.4;
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'signing'
    apply plugin: 'java'
    apply plugin: 'maven'

    project.group = 'org.develnext'
    project.version = projectVersion

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }

    task sourcesJar(type: Jar, dependsOn: 'classes') {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

configure([
        project(':jphp-gui-ext'),
        project(':jphp-gui-designer-ext'),
        project(':jphp-gui-framework')
]) {
    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    javadoc.failOnError = false

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    jar.dependsOn += [sourcesJar, javadocJar]

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    signing {
        sign configurations.archives
    }

    bintray {
        user = rootProject.hasProperty('bintray.user') ? rootProject.getProperty("bintray.user") : "";
        key = rootProject.hasProperty('bintray.apikey') ? rootProject.getProperty("bintray.apikey") : "";

        configurations = ['archives']

        pkg {
            repo = "maven"
            name = "jphp-compiler"
            websiteUrl = 'http://github.com/jphp-compiler'
            vcsUrl = 'http://github.com/jphp-compiler'
            licenses = ["Apache-2.0"]
            publish = true
        }
    }
}

project(':jphp-gui-ext') {
    dependencies {
        compile "org.develnext:jphp-runtime:$jphpVersion"
    }
}

project(':jphp-parser') {
    dependencies {
        compile "org.develnext:jphp-core:$jphpVersion"
    }
}

project(':jphp-gui-framework') {
    dependencies {
        compile project(':jphp-gui-ext')
        compile "org.develnext:jphp-core:$jphpVersion"
    }
}

project(':develnext') {
    dependencies {
        compile project(':jphp-gui-designer-ext')
        compile project(':jphp-gui-framework')
        compile project(':jphp-parser')
        compile project(':develnext-stdlib')

        compile "org.develnext:jphp-xml-ext:$jphpVersion"
        compile "org.develnext:jphp-json-ext:$jphpVersion"
        compile "org.develnext:jphp-jsoup-ext:$jphpVersion"
        compile "org.develnext:jphp-sql-ext:$jphpVersion"
        compile "org.develnext:jphp-compress-ext:$jphpVersion"
        compile "org.develnext:jphp-debugger:$jphpVersion"
    }
}

