apply plugin: 'application'

project.version = '1.0'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()

    repositories {
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
}

sourceSets {
    main.resources.srcDirs = ['src']
}

run {
    mainClassName = 'php.runtime.launcher.Launcher'
    jvmArgs += ["-Dfile.encoding=UTF-8", "-Xms256m", "-Xms1280m"]
}

jar.archiveName = 'DevelNext.jar'

manifest {
    attributes 'Main-Class': 'php.runtime.launcher.Launcher'
}

installDist.doLast {
    fileTree("$project.buildDir/install/$project.name/lib").visit { FileVisitDetails el ->
        def name = el.getName()
        name = name.replaceAll(/(-[0-9]+|\.[0-9]+|-SNAPSHOT)/, "");

        if (name.startsWith("develnext-control-")) {
            el.file.delete();
        } else {
            if (!name.equals(el.getName())) {
                println "Rename '" + el.file.parent + "/" + name + "' file"

                el.file.renameTo(file(el.file.parent + "/" + name));
            }
        }
    }
}

task distWindowsPatch(type: Zip, dependsOn: ['clean', 'distPatch']) {
    from "$project.buildDir/install/$project.name"
    archiveName "DevelNext.WindowsPatch.zip"

    doLast {
        def issFile = "$project.rootDir/develnext/windowsSetup/innopatchsetup.iss".replace("\\", "/")
        def innoSetupBin = "$project.rootDir/develnext-tools/innoSetup/ISCC.exe".replace("\\", "/")

        exec {
            commandLine innoSetupBin, '/Qp', issFile
        }

        def uploadPath = System.getProperty("uploadPath", "E:/Upload")

        if (new File(uploadPath).isDirectory()) {
            copy {
                from "$project.buildDir/distributions/DevelNext.WindowsPatch.exe"
                into "$uploadPath/YandexDisk"
            }

            copy {
                from distWindowsPatch.archivePath
                into "$uploadPath/YandexDisk"
            }
        }
    }
}

task distLinuxPatch(type: Tar, dependsOn: ['clean', 'distPatch']) {
    def installDir = "$project.buildDir/install/$project.name"

    compression = Compression.GZIP
    archiveName "DevelNext.LinuxPatch.tar.gz"

    from(installDir) {
        exclude('*.sh')
        exclude('DevelNext')
        exclude('bin/develnext')
        exclude('tools/gradle/bin/gradle')
        exclude('tools/jre/bin/java')
        exclude('tools/Launch4jLinux/launch4j')
    }

    from(installDir) {
        include('*.sh')
        include('DevelNext')
        include('bin/develnext')
        include('tools/gradle/bin/gradle')
        include('tools/jre/bin/java')
        include('tools/Launch4jLinux/launch4j')

        fileMode = 0755
    }

    doLast {
        def uploadPath = System.getProperty("uploadPath", "E:/Upload")

        if (new File(uploadPath).isDirectory()) {
            copy {
                from distLinuxPatch.archivePath
                into "$uploadPath/YandexDisk"
            }
        }
    }
}


task distPatch(dependsOn: ['clean', 'installDist']) {
    doLast {
        if (System.properties['os.name'].toLowerCase().contains('windows')) {
            exec {
                commandLine "$project.rootDir/develnext-tools/Launch4j/launch4jc.exe"
                args "$project.rootDir/develnext/launcher/winLauncher.xml"
            }
        }

        copy {
            from "$project.rootDir/develnext/launcher/DevelNext.exe"
            into "$project.buildDir/install/$project.name"
        }

        copy {
            from "$project.rootDir/develnext/misc"
            into "$project.buildDir/install/$project.name"
        }
    }
}


task distIdeWindows(dependsOn: ['distIde']) {
    doLast {
        delete "$project.buildDir/install/$project.name/tools/jreLinux"
        delete "$project.buildDir/install/$project.name/tools/Launch4jLinux"
    }
}

task distIdeLinux(dependsOn: ['distIde']) {
    doLast {
        delete "$project.buildDir/install/$project.name/tools/jre"
        delete "$project.buildDir/install/$project.name/tools/Launch4j"
        file("$project.buildDir/install/$project.name/tools/jreLinux").renameTo(file("$project.buildDir/install/$project.name/tools/jre"))
    }
}

task distIde(dependsOn: ['installDist']) {
    doLast {
        if (System.properties['os.name'].toLowerCase().contains('windows')) {
            exec {
                commandLine "$project.rootDir/develnext-tools/Launch4j/launch4jc.exe"
                args "$project.rootDir/develnext/launcher/winLauncher.xml"
            }
        } else if (System.properties['os.name'].toLowerCase().contains('linux')) {
            exec {
                commandLine "$project.rootDir/develnext-tools/Launch4jLinux/launch4j"
                args "$project.rootDir/develnext/launcher/winLauncher.xml"
            }
        }

        copy {
            from "$project.rootDir/develnext-tools"
            into "$project.buildDir/install/$project.name/tools"
        }

        copy {
            from "$project.rootDir/develnext/launcher/DevelNext.exe"
            into "$project.buildDir/install/$project.name"
        }

        copy {
            from "$project.rootDir/develnext/misc"
            into "$project.buildDir/install/$project.name"
        }
    }
}

task distIdeWindowsSetup(dependsOn: ['distIdeWindows']) {
    doLast {
        def issFile = "$project.rootDir/develnext/windowsSetup/innosetup.iss".replace("\\", "/")
        def innoSetupBin = "$project.rootDir/develnext-tools/innoSetup/ISCC.exe".replace("\\", "/")

        exec {
            commandLine innoSetupBin, '/Qp', issFile
        }

        def uploadPath = System.getProperty("uploadPath", "E:/Upload")

        if (new File(uploadPath).isDirectory()) {
            copy {
                from "$project.buildDir/distributions/DevelNextSetup.exe"
                into "$uploadPath/YandexDisk"
            }
        }
    }
}

task distIdeLinuxSetup(type: Tar, dependsOn: ['distIdeLinux']) {
    def installDir = "$project.buildDir/install/$project.name"

    compression = Compression.GZIP
    archiveName "DevelNextLinux.tar.gz"

    from(installDir) {
        exclude('*.sh')
        exclude('DevelNext')
        exclude('bin/develnext')
        exclude('tools/gradle/bin/gradle')
        exclude('tools/jre/bin/java')
        exclude('tools/Launch4jLinux/launch4j')
    }

    from(installDir) {
        include('*.sh')
        include('DevelNext')
        include('bin/develnext')
        include('tools/gradle/bin/gradle')
        include('tools/jre/bin/java')
        include('tools/Launch4jLinux/launch4j')

        fileMode = 0755
    }

    doLast {
        def uploadPath = System.getProperty("uploadPath", "E:/Upload")

        if (new File(uploadPath).isDirectory()) {
            copy {
                from distIdeLinuxSetup.archivePath
                into "$uploadPath/YandexDisk"
            }
        }
    }
}
